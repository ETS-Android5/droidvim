apply plugin: 'com.android.application'
repositories {
    maven { url "https://jitpack.io" }
}

import java.text.SimpleDateFormat

android {
    compileSdkVersion versions.compileSdk

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
    }

    defaultConfig {
        versionName = "1.0.0"
        versionCode 1

        buildConfigField "String", "WHATS_NEW", '""'
        buildConfigField "String", "BASE64_PUBLIC_KEY", String.format('"%s"', file('base64-public-key.txt').getText().replaceAll("[\r\n].*", ""))

        externalNativeBuild {
            ndkBuild {
                arguments '-C', 'src/main', '-j', Runtime.runtime.availableProcessors() + ''
                abiFilters 'armeabi-v7a', 'x86', 'arm64-v8a', 'x86_64'
            }
        }

        vectorDrawables.useSupportLibrary = true
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }

    sourceSets {
        origin {
            jniLibs.srcDirs = ['src/main/jniLibs']
        }
        droidvim {
            jniLibs.srcDirs = ['src/droidvim/jniLibs']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "default"
    productFlavors {
        origin {
            dimension "default"
            versionName = new SimpleDateFormat("yyyy.M.dd.HH.mm.ss").format(new Date()) + ".dev"
        }
        droidvim {
            dimension "default"
            applicationId "com.droidvim"
        }
    }

    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0-rc01'
    implementation 'com.google.android.material:material:1.0.0'
}

dependencies {
    implementation project(':emulatorview')
    implementation project(':libtermexec')
}

dependencies {
    implementation 'org.apache.commons:commons-compress:1.18'
    implementation 'commons-io:commons-io:2.6'
    implementation 'org.tukaani:xz:1.8'
    implementation 'com.github.hedzr:android-file-chooser:1.1.11'
}

