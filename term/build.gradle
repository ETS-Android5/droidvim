apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion versions.compileSdk

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        multiDexEnabled true
    }

    defaultConfig {
        versionName versions.versionName
        versionCode versions.versionCode

        buildConfigField "String", "WHATS_NEW", '""'
        buildConfigField "String", "BASE64_PUBLIC_KEY", String.format('"%s"', file('base64-public-key.txt').getText().replaceAll("[\r\n].*", ""))

    }

    defaultConfig {
        externalNativeBuild {
            ndkBuild {
                arguments '-C', 'src/main', '-j', Runtime.runtime.availableProcessors() + ''
                if (abiArm) abiFilters 'arm64-v8a', 'armeabi-v7a'
                if (abiX86) abiFilters 'x86_64', 'x86'
            }
        }

        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        sourceCompatibility = versions.sourceCompatibility
        targetCompatibility = versions.targetCompatibility
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    splits {
        abi {
            enable false
            reset()
            if (abiArm) include 'arm64-v8a', 'armeabi-v7a'
            if (abiX86) include 'x86_64', 'x86'
            universalApk true
        }
    }

    flavorDimensions "appType"
    productFlavors {
        terminal {
            dimension "appType"
        }
        droidvim {
            dimension "appType"
            applicationId "com.droidvim"
        }
    }

    sourceSets {
        terminal {
            res.srcDirs += ['src/terminal-res/res']
            jniLibs.srcDirs = ['src/main/jniLibs']
            if (abiArm) {
                res.srcDirs += ['src/main-arm/res']
                jniLibs.srcDirs += ['src/main-arm/jniLibs']
            }
            if (abiX86) {
                res.srcDirs += ['src/main-x86/res']
                jniLibs.srcDirs += ['src/main-x86/jniLibs']
            }
        }
        droidvim {
            res.srcDirs += ['src/droidvim/res', 'src/vim/res']
            jniLibs.srcDirs += ['src/main/jniLibs']
            if (abiArm) {
                res.srcDirs += ['src/main-arm/res', 'src/vim-arm/res']
                jniLibs.srcDirs += ['src/main-arm/jniLibs', 'src/vim-arm/jniLibs']
            }
            if (abiX86) {
                res.srcDirs += ['src/main-x86/res', 'src/vim-x86/res']
                jniLibs.srcDirs += ['src/main-x86/jniLibs', 'src/vim-x86/jniLibs']
            }
        }
    }

}

dependencies {
    implementation 'androidx.multidex:multidex:2.0.1'
}

dependencies {
    implementation "androidx.core:core-ktx:${versions.core_ktx}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"
}

// Libraries which can be re-used in other modules should use the `api` keyword.
// This way they can be shared with dependent feature modules.
dependencies {
    api "androidx.annotation:annotation:${versions.annotation}"
    api "androidx.appcompat:appcompat:${versions.appcompat}"
    api "androidx.constraintlayout:constraintlayout:${versions.constraintlayout}"
    api "androidx.documentfile:documentfile:${versions.documentfile}"
    api "androidx.preference:preference-ktx:${versions.preference}"
    api "androidx.vectordrawable:vectordrawable:${versions.vectordrawable}"
    api "com.google.android.material:material:${versions.material}"
}

dependencies {
    implementation 'org.apache.commons:commons-compress:1.18'
    implementation 'commons-io:commons-io:2.6'
    implementation 'org.tukaani:xz:1.8'
}

dependencies {
    implementation project(':emulatorview')
    implementation project(':libtermexec')
}

