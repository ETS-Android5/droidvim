apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
repositories {
    mavenCentral()
}

import java.text.SimpleDateFormat

android {
    compileSdkVersion versions.compileSdk

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
    }

    defaultConfig {
        versionName = "1.0.0"
        versionCode versions.versionCode

        buildConfigField "String", "WHATS_NEW", '""'
        buildConfigField "String", "BASE64_PUBLIC_KEY", String.format('"%s"', file('base64-public-key.txt').getText().replaceAll("[\r\n].*", ""))

    }

    defaultConfig {
        externalNativeBuild {
            ndkBuild {
                arguments '-C', 'src/main', '-j', Runtime.runtime.availableProcessors() + ''
                abiFilters 'arm64-v8a', 'armeabi-v7a'
                abiFilters 'x86_64', 'x86'
            }
        }

        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    splits {
        abi {
            enable false
            reset()
            include 'arm64-v8a', 'armeabi-v7a'
            include 'x86_64', 'x86'
            universalApk true
        }
    }

    flavorDimensions "default"
    productFlavors {
        origin {
            dimension "default"
            versionName = new SimpleDateFormat("yyyy.M.dd.HH.mm.ss").format(new Date()) + ".dev"
        }
        droidvim {
            dimension "default"
            applicationId "com.droidvim"
        }
    }

    sourceSets {
        origin {
            jniLibs.srcDirs = ['src/main/jniLibs']
            res.srcDirs += ['src/main-arm/res']
            res.srcDirs += ['src/main-x86/res']
        }
        droidvim {
            jniLibs.srcDirs += ['src/main/jniLibs']
            jniLibs.srcDirs += ['src/main-arm/jniLibs']
            jniLibs.srcDirs += ['src/main-x86/jniLibs']
            res.srcDirs += ['src/droidvim/res', 'src/vim/res']
            res.srcDirs += ['src/main-arm/res', 'src/vim-arm/res']
            res.srcDirs += ['src/main-x86/res', 'src/vim-x86/res']
        }
    }

}

dependencies {
    implementation "androidx.core:core-ktx:$version_core_ktx"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$version_kotlin"
}

dependencies {
    implementation "androidx.annotation:annotation:$version_annotation"
    implementation "androidx.appcompat:appcompat:$version_appcompat"
    implementation "androidx.constraintlayout:constraintlayout:$version_constraintlayout"
    implementation "androidx.documentfile:documentfile:$version_documentfile"
    implementation "androidx.preference:preference:$version_preference"
    implementation "androidx.vectordrawable:vectordrawable:$version_vectordrawable"
    implementation "com.google.android.material:material:$version_material"
}

dependencies {
    implementation project(':emulatorview')
    implementation project(':libtermexec')
}

dependencies {
    implementation 'org.apache.commons:commons-compress:1.18'
    implementation 'commons-io:commons-io:2.6'
    implementation 'org.tukaani:xz:1.8'
}

